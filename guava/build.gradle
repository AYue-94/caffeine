/**
 * Guava compatibility adapter.
 *
 * The tests are forked from Guava commit e370dde.
 */
dependencies {
  api project(':caffeine')
  api libraries.guava

  testImplementation libraries.slf4jNop
  testImplementation testLibraries.jctools
  testImplementation testLibraries.guavaTestLib
}

tasks.named('forbiddenApisTest').configure {
  bundledSignatures -= 'jdk-reflection'
}

tasks.named('compileJava').configure {
  modularity.inferModulePath = true
}

tasks.named('jar').configure {
  bundle.bnd(
    'Bundle-SymbolicName': 'com.github.ben-manes.caffeine.guava',
    'Import-Package': [
      'com.google.common.cache',
      'com.google.common.util.concurrent',
      'com.github.benmanes.caffeine.cache',
      'com.github.benmanes.caffeine.cache.stats'].join(','),
    'Export-Package': 'com.github.benmanes.caffeine.guava',
    'Automatic-Module-Name': 'com.github.benmanes.caffeine.guava')
}

tasks.withType(Javadoc).configureEach {
  options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named('test').configure {
  useJUnitPlatform()
  systemProperties [
    'guava.osgi.version': versions.guava,
    'caffeine.osgi.jar': relativePath(project(':caffeine').jar.archivePath.path),
    'caffeine-guava.osgi.jar': relativePath(project(':guava').jar.archivePath.path),
  ]
}
